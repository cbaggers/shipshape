(in-package :shipshape)

;; - can be any type accepted by #'pathname

;; the system does the following
;; - get the absolute path relative to the system
;; - check if #'cl-fad:directory-exists-p
;;   - if yes then copy all containing files recursively
;;   - if no then check #'cl-fad:file-exists-p
;;      - if yes then copy file
;;      - if no then throw error

(defun validate-and-process-copy-paths (system paths)
  ;; We do this together so we can aggregate the error message
  ;; this makes it less tedious to fix
  (labels ((path (p) (local-path p system)))
    (let* ((absolute-paths (mapcar #'path paths))
	   (exist (mapcar (lambda (p)
			    (or (cl-fad:file-exists-p p)
				(cl-fad:directory-exists-p p)))
			  absolute-paths))
	   (problem (remove nil (mapcar (lambda (e a) (unless e a))
					exist absolute-paths))))
      (if problem
	  (error "Could not proceed with build as the following files/directories
don't exist:簋痱镡戾愆徕箫祯翦疳翳螬┅ㄤ彐躅泔瘗犰飙礤溟磲铋驽篝ㄡ篌弪铒箬轲疱洫┅鏖翳箪雉簌篝屙泔瘗疳翳螬磲铋驽篝戾è疳翳鲠扉溽翦犷洵痱镢弩蟓泔瘗疳翳簌篝屙泔瘗疳翳螬┅磲铋灬礅溽皓ㄣ镳礤溟磲铋驽篝┅疳翳螬┅ㄤ彐躅泔瘗礤溟疳翳磲铋驽篝ㄣ镱è沆驷浜溟蝈泗矧屮轶趔疳翳ㄣ镳溟疳翳磲铋驽篝┅è沆驷浜骈戾屮轶趔疳翳ㄣ镳骈戾疳翳磲铋驽篝┅ㄥ蝌矧⑸铞犰殇疳翳簋ピ栝轶怩绗痨遽箦蝈痫螋轸狒湖ヨ趑痼函玳翳踱泔懑汊徵珏蝮箬轲箬狃瀵轶篚弩疳翳┅┅ㄤ彐躅泔瘗骈戾疳翳磲铋驽篝鏖翳箪雉簌篝屙簌篝屙礤溟岘疳翳磲铋驽篝戾舄è篁祜汜飙疳翳疳翳簌篝屙┅ㄤ篝蝈漉沐＇灬礅溽礤蜱瀛疳翳钺礤┅扉篝ㄦ矧磲铋岑簌篝屙疳翳钺礤骈戾钺礤篁悌洪铋糸犰鲠祯祜汜飙簌篝屙礤溟岘疳翳磲铋驽篝┅┅ㄥ铙躜瀛溟蝈泗矧殄蟓屮轶潴舂ㄣ飙驷浜泔瘗骈戾篁潴舂┅ㄤ彐躅泔瘗溟疳翳磲铋驽篝ㄣ飙驷浜麽祀溟蝈泗矧疳翳灬礅溽ㄣ镳骈戾磲铋驽篝┅┅